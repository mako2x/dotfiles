if ! which peco > /dev/null; then
  return
fi

# History
function peco-history() {
  typeset tac
  if which tac > /dev/null; then
    tac=tac
  else
    tac='tail -r'
  fi
  BUFFER=$(fc -l -n 1 | eval $tac | peco --query "$LBUFFER")
  CURSOR=$#BUFFER
  zle -R -c
}
zle -N peco-history
bindkey -M afu "^R" peco-history


# Snippets
function peco-snippets() {
  BUFFER=$(grep -v "^#" ~/.snippets | peco --query "$LBUFFER")
  zle clear-screen
}
zle -N peco-snippets
bindkey -M afu "^S" peco-snippets


# integrate all source code with ghq
function peco-src() {
  local selected_dir=$(ghq list | peco --query "$LBUFFER")
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${GOPATH}/src/${selected_dir}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-src
stty -ixon
bindkey -M afu '^O' peco-src


# Kill process
function peco-pkill() {
  for pid in `ps aux | peco | awk '{ print $2 }'`
  do
    kill $pid
    echo "Killed ${pid}"
  done
}

# Alias
alias pkill="peco-pkill"
alias -g B='`git branch | peco | sed -e "s/^\*[ ]*//g"`'
